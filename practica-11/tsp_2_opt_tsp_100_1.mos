!@encoding CP1252
model "tsp_2_opt_tsp_100_1"
	uses "mmive";
	
	! DECLARACIONES 1
	declarations
		n : integer;
	end-declarations

	! INICIALIZACION 1
	fopen("data/tsp_100_1.txt", F_INPUT);
	read(n);

	! DECLARACIONES 2
	declarations
		p_nodos		= 1..n;

		coords		: array(p_nodos, 1..3) of real;

		distancias	: array(p_nodos, p_nodos) of integer;
		visitados	: array(p_nodos) of integer;
		siguientes	: array(p_nodos) of integer;
		siguientes2	: array(p_nodos) of integer;
	end-declarations

	! INICIALIZACION 2
	forall(i in p_nodos) do
		read(coords(i,1));
		read(coords(i,2));
		read(coords(i,3));
	end-do
	fclose(F_INPUT);

	! Calculamos las distancias mediante las coordenadas leidas
	forall(i in p_nodos, j in p_nodos)
		if (i <> j) then
			distancias(i,j) := round(sqrt( (coords(i,2) - coords(j,2))^2 + (coords(i,3) - coords(j,3))^2 ));
		else
			distancias(i,j) := MAX_INT;
		end-if

	fclose(F_INPUT);


	!==============================================================================
	! SOLUCION TSP GREEDY
	!==============================================================================
	nodo_inicial	:= ceil(n * random);
	nodo_anterior	:= nodo_inicial;
	visitados(nodo_inicial) := 1;
	num_nodos := 1;

	distancia_total := 0;
	while (num_nodos < n) do
		! Buscamos el i m�s cercano
		aux := MAX_INT;
		forall(i in p_nodos | visitados(i) <> 1) do
			if(distancias(nodo_anterior, i) < aux) then
				aux := distancias(nodo_anterior, i);
				i_cercano := i;
			end-if
		end-do

		! Introducimos el i aleatorio en la solucion
		siguientes(nodo_anterior) := i_cercano;
		visitados(i_cercano) := 1;
		distancia_total += distancias(nodo_anterior, i_cercano);
		num_nodos += 1;

		nodo_anterior := i_cercano;
	end-do

	! A�adimos la conexi�n entre el �ltimo nodo y el primero
	siguientes(nodo_anterior) := nodo_inicial;
	distancia_total += distancias(nodo_anterior, nodo_inicial);


	!==============================================================================
	! MEJORA 2-OPT
	!==============================================================================
	flag1 := 0;
	while(flag1 = 0) do
		mejora_max := 0;
		i_intercambio := -1; j_intercambio := -1;

		forall(	i in p_nodos, j in p_nodos |
				i <> j and siguientes(i) <> j and siguientes(j) <> i ) do
			! Calculamos la mejora del intercambio
			s_i := siguientes(i);
			s_j := siguientes(j);
			delta_ij := distancias(i,s_i) + distancias(j,s_j) -
						distancias(i,j) - distancias(s_i,s_j);

			if(delta_ij > mejora_max) then
				mejora_max := delta_ij;
				i_intercambio := i;
				j_intercambio := j;
			end-if
		end-do

		if(mejora_max = 0) then
			! Salimos del bucle sin hacer el intercambio
			flag1 := 1;
		else
			! Realizamos el intercambio
			siguientes2 := siguientes;

			siguientes(i_intercambio) := j_intercambio;
			siguientes(siguientes2(i_intercambio)) := siguientes2(j_intercambio);

			actual := siguientes2(i_intercambio);
			while(actual <> j_intercambio) do
				siguiente := siguientes2(actual);
				siguientes(siguiente) := actual;
				actual := siguiente;
			end-do
			
			! Actualizamos la distancia total
			distancia_total -= mejora_max;
		end-if
	end-do


	! SALIDA
	actual := 1;
	writeln("Problema TSP (n = ", n, ")");
	writeln("Distancia Total: ", distancia_total);
	write("Conexiones: ", actual);
	
	arrows := IVEaddplot("arrows", IVE_BLUE);
	labels := IVEaddplot("labels", IVE_WHITE);
	IVEdrawlabel(labels, coords(actual,2), coords(actual,3), ""+actual);

	while(siguientes(actual) <> 1) do
		siguiente := siguientes(actual);
		write(" -> ", siguiente);
		IVEdrawarrow(arrows, coords(actual,2), coords(actual,3), coords(siguiente,2), coords(siguiente,3));
		IVEdrawlabel(labels, coords(siguiente,2), coords(siguiente,3), ""+siguiente);
		actual := siguiente;
	end-do
end-model
