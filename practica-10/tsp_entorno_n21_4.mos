!@encoding CP1252
model "tsp_entorno_n21_4"
	! DECLARACIONES 1
	declarations
		n : integer;
	end-declarations
	
	! INICIALIZACION 1
	fopen("data/n21_4.dat", F_INPUT);
	read(n);
	
	! DECLARACIONES 2
	declarations
		p_nodos	= 1..n;
		
		distancias	: array(p_nodos, p_nodos) of integer;
		visitados	: array(p_nodos) of integer;
		siguientes	: array(p_nodos) of integer;
	end-declarations
	
	! INICIALIZACION 2
	forall(i in p_nodos, j in p_nodos)
		read(distancias(i,j));
	fclose(F_INPUT);
	
	
	!==============================================================================
	! SOLUCION TSP GREEDY
	!==============================================================================
	nodo_inicial	:= ceil(n * random);
	nodo_anterior	:= nodo_inicial;
	visitados(nodo_inicial) := 1;
	num_nodos := 1;
	
	distancia_total := 0;
	while (num_nodos < n) do
		! Buscamos el i más cercano
		aux := MAX_INT;
		forall(i in p_nodos | visitados(i) <> 1) do
			if(distancias(nodo_anterior, i) < aux) then
				aux := distancias(nodo_anterior, i);
				i_cercano := i;
			end-if
		end-do
		
		! Introducimos el i aleatorio en la solucion
		siguientes(nodo_anterior) := i_cercano;
		visitados(i_cercano) := 1;
		distancia_total += distancias(nodo_anterior, i_cercano);
		num_nodos += 1;
		
		nodo_anterior := i_cercano;
	end-do
	
	! Añadimos la conexión entre el último nodo y el primero
	siguientes(nodo_anterior) := nodo_inicial;
	distancia_total += distancias(nodo_anterior, nodo_inicial);
	
	
	! SALIDA
	actual := 1;
	writeln("Problema TSP (n = ", n, ")");
	writeln("Distancia Total: ", distancia_total);
	write("Conexiones: ", actual);
	while(siguientes(actual) <> 1) do
		siguiente := siguientes(actual);
		write(" -> ", siguiente);
		actual := siguiente;
	end-do
end-model
