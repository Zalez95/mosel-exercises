!@encoding CP1252
model "5_1_p_centro"
	uses "mmxprs";
	
	! DECLARACIONES
	declarations
		n 				= 12;
		m 				= 12;
		p_servicio		= 1..n;
		p_demanda		= 1..m;
		
		t				: array(p_demanda, p_servicio) of integer;
		
		y				: array(p_demanda, p_servicio) of mpvar;
		x				: array(p_servicio) of mpvar;
		w				: mpvar;
	end-declarations
	
	! INICIALIZACION
	initializations from "data/5_1_p_centro.dat"
		t;
	end-initializations
	
	! MODELO
	! Variables binarias para x e y
	forall(j in p_servicio)
		x(j) is_binary;
	forall(i in p_demanda, j in p_servicio)
		y(i,j) is_binary;
	
	! Puntos de demanda cubiertos
	forall(i in p_demanda)
		cubrimiento(i) := sum(j in p_servicio) y(i,j) = 1;
	
	! Tiempo de respuesta
	forall(i in p_demanda)
		t_respuesta(i) := sum(j in p_servicio) t(i,j) * y(i,j) <= w;
	
	forall(i in p_demanda, j in p_servicio)
		res(i,j) := y(i,j) <= x(j);
	
	writeln("Práctica 5.1 P-centro (n=", n, ", m=", m, "):");
	forall(p in 1..12) do
		! Numero de instalaciones
		num_instalaciones := sum(j in p_servicio) x(j) = p;
		minimize(w);
	
		! SALIDA
		writeln("p = ", p, "\tValor objetivo w = ", getobjval);
	end-do	
end-model
