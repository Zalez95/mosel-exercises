!@encoding CP1252
model "publicidad"
	uses "mmxprs";
	
	! DECLARACIONES
	declarations
		n			= 50;
		m			= 10;
		p_demanda	= 1..m;
		p_servicio	= 1..n;
		
		a			: array(p_demanda, p_servicio) of integer;
		
		x			: array(p_servicio)	of mpvar;
		z			: array(p_demanda)	of mpvar;
	end-declarations
		
	! DATOS
	initializations from "data/publicidad.dat"
		a;
	end-initializations
	
	! MODELO
	forall(i in p_demanda)
		recubrimiento(i) := sum(j in p_servicio) a(i,j)*x(j) >= z(i);
	forall(j in p_servicio)	x(j) is_binary;
	forall(i in p_demanda)	z(i) is_binary;
	
	dem_cub := sum(i in p_demanda) z(i);
	
	writeln("Problema de cubrimiento de publicidad (n = ", n, ", m = ", m, "):\n");
	forall(P in 1..10)do
		rest := sum(j in p_servicio) x(j) = P;
		maximize(dem_cub);
	
		! SALIDA
		writeln("P = ", P, ",\tPorcentaje cubierto: ", 100 * getobjval / n, "%");
		write("\tPuntos cubiertos:");
		forall(i in p_demanda | z(i).sol = 1)
			write(" ", i);
		write("\n\tPuntos instalados:");
		forall(j in p_servicio | x(j).sol = 1)
			write(" ", j);
		write("\n");
	end-do	
end-model
